local -a globout           # Unprocessed output
local -a output            # Processed output
local -a nonselectables    # To be appended, upon making indices global, to ZCMDRLIST_NONSELECTABLE_ELEMENTS
local -a hop_indices       # To be appended, upon making indices global, to ZCMDRLIST_HOP_INDEXES
integer own_LINE_COUNT=0   # Local line count

globout=( "$@" )
size="${#globout[@]}"

output=( "${CMDR_RED}'Tree (select to open) of ${CMDR_RED_E}${CMDR_YELLOW}${current_project:t}${CMDR_YELLOW_E}${CMDR_RED}'${CMDR_RED_E}" )
own_LINE_COUNT+=1
nonselectables+=( "$own_LINE_COUNT" )
hop_indices+=( "$own_LINE_COUNT" )

#
# Create output
#

for (( idx = 1; idx <= size; idx ++ )); do
    local txt="${globout[idx]/(#s)$current_project\//}"
    txt="${txt/(#s)$HOME/~}"
    local passed_path="${globout[idx]/(#s)$HOME/~}"

    reply=( )
    __zcmdr_stdlib_get_std_button "topenA" "" "$selected_editor" "$current_project" "$passed_path" "$txt" __zcmdr_action_tree_open
    output+=( "${reply[1]}" )
    own_LINE_COUNT+=1
done

# Button to load tree when having no results
if [[ "$size" -eq "0" ]]; then
    reply=( )
    __zcmdr_stdlib_get_button "" ",tree," "/Load if ready/" "load" "" "Load" ""
    output+=( "${reply[1]}" )
    own_LINE_COUNT+=1
fi

reply=( "${output[@]}" )
reply2="$own_LINE_COUNT"
reply3=( "${nonselectables[@]}" )
reply4=( "${hop_indices[@]}" )

# vim:ft=zsh
